#!/usr/bin/perl
# Output recently installed (or otherwise manipulated) Debian packages in
# format usable by 'apt-get remove'.
# Usage: apt-history
#        zcat /var/log/apt/history.log.1.gz | apt-history -
use strict;
use warnings;

# Default log file
push @ARGV, '/var/log/apt/history.log' unless @ARGV;

my ( $in_block, $date );
while (<>) {
    if (/^Start-Date:\s*(\S.*)$/) {
        $date     = $1;
        $in_block = 1;
        next;
    } elsif (/^End-Date:/) {
        $in_block = 0;
        next;
    }
    if ($in_block) {
        next if /^Commandline:/;
        if (/^([^:]+):(.*)$/) {
            my $operation = $1;
            my @pkgs = split /,/, $2;
            print "---> $operation on $date\n";
            for (@pkgs) {
                print "$1 " if /\s*([^:]+):/;
            }
            print "\n";
        }
    }
}

