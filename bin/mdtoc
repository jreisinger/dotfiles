#!/usr/bin/env perl
#
# Create table of contents from markdown hash style (#) headers.
#
use 5.014;    # implies "use strict;"
use warnings;
use autodie;

my $file = shift;
die "Usage: $0 file.md\n" unless $file;

my @TOC;
my $in_code;

open( my $fh, $file );

while (<$fh>) {
    if ( /^```/ and not $in_code ) {
        $in_code = 1;
        next;
    }

    if ( /^```/ and $in_code ) {
        $in_code = 0;
        next;
    }

    if ( /^(#+\s[A-Z].*)$/ and not $in_code ) {
        my $title = $&;
        my $link  = $&;

        # count hashes in header (#, ##, ###, ...)
        my $hash_count = () = $title =~ /#/g;

        $title =~ s/[\s#]+//;

        $link =~ s/[\s#]+/#/;
        $link = lc $link;
        $link =~ s/\s+/-/g;
        $link =~ s/[\(\)\@]//g;

        if ( $hash_count <= 2 ) {    # max indent level is 2
            push @TOC, "    " x ( $hash_count - 1 ) . "* [$title]($link)";
        }
    }
}

print join "\n", @TOC;
print "\n";
