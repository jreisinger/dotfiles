#!/usr/bin/perl -s
use strict;
use warnings;
use vars qw($h $w $s);       # command line switches
use LWP::Protocol::https;    # for SSL (https) support
use LWP::Simple;
use CGI;

$|++;

usage() if $h;

my $url =
  'https://raw.githubusercontent.com/jreisinger/blog/master/posts/quotes.txt';
my $doc = get $url;
unless ($doc) {
    getprint $url;
    die "Exiting ...\n";
}

my @quotes = split /\n+/, $doc;
my $quote = $quotes[ rand @quotes ];

binmode STDOUT, ':utf8';
if ($w) {

    # -- TimToady (http://irclog.perlgeek.de/perl6/2013-07-21)
    # ===>
    # -- <a href="http://irclog.perlgeek.de/perl6/2013-07-21">TimToady</a>
    $quote =~
      s#--\s{0,3}([^(]+?)\s+\((http:\/\/[^)]+)\)#-- <a href="$2">$1</a>#;

    my $q = CGI->new;

    #<<<
    print 
      $q->start_html( -title => "Quote of the Minute", -encoding => "utf-8" ),
      $q->p($quote),
      #$q->img(
      #  {
      #      src => "http://imgs.xkcd.com/comics/lisp.jpg",
      #      title =>
      #        "We lost the documentation on quantum mechanics.  You'\''ll have to decode the regexes yourself.",
      #      alt => "Lisp"
      #  }
      #),
      $q->end_html;
    #>>>
} else {
    if ($s) {
        slowprint( $quote . "\n" );
    } else {
        print $quote . "\n";
    }
}

#######################################
sub slowprint {
#######################################
    my $text = shift;
    for my $char ( split //, $text ) {
        print $char;
        select( undef, undef, undef, 0.1 );
    }
}

#######################################
sub usage {
#######################################
    my $code = (shift) // 0;

    my $msg = <<"EOF";
$0 [-h] [-w|-s]
Print a random quote from my collection of quotes
-w  in HTML format
-s  print characters slowly; ignored when -w is used

Usage from cron:
PERL5LIB=\$HOME/perl5/MyUtils/lib:\$HOME/perl5/lib/perl5
* * * * *   \$HOME/bin/myquote > \$HOME/public_html/quote.html
EOF

    print $msg;

    exit $code;
}
