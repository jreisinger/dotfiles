#!/usr/bin/env perl
# Install useful vim plugins.
# Usage: run this script.
use strict;
use warnings;
use File::Path qw(make_path remove_tree);
use IPC::Cmd qw(can_run);

sub _get_web {
    my ( $remote, $local ) = @_;

    # If curl is not there use wget
    print "Donwloading $remote to '$local'\n";
    if ( can_run('curl') ) {
        system "curl -so $local $remote";
    } else {
        system "wget -q -O $local $remote";
    }
}

sub _get_git {
    my ( $remote_repo, $local_repo ) = @_;

    if ( -d $local_repo ) {
        chdir $local_repo and print "Changed to '$local_repo'\n";
        system "git pull";
    } else {
        system "git clone $remote_repo $local_repo";
    }
}

# MAIN

# Create necessary directories
my $autoload_dir = "$ENV{HOME}/.vim/autoload";
make_path $autoload_dir unless -e $autoload_dir;
my $bundle_dir = "$ENV{HOME}/.vim/bundle";
make_path $bundle_dir unless -d $bundle_dir;

my $plugins = [
    {
        name => 'pathogen.vim',
        remote =>
'https://raw.github.com/tpope/vim-pathogen/master/autoload/pathogen.vim',
        local => "$autoload_dir/pathogen.vim",
    },
    {
        name   => 'vim nerd tree',
        remote => 'https://github.com/scrooloose/nerdtree.git',
        local  => "$bundle_dir/nerdtree",
    },
    {
        name   => 'vim-markdown',
        remote => 'https://github.com/plasticboy/vim-markdown.git',
        local  => "$bundle_dir/vim-markdown",
    },
    {
        name   => 'BufExplorer',
        remote => 'https://github.com/jlanzarotta/bufexplorer.git',
        local  => "$bundle_dir/bufexplorer",
    },
];

for my $plugin (@$plugins) {
    print "\n--> Install $plugin->{name}\n";
    if ( $plugin->{remote} =~ /\.git$/ ) {
        _get_git( $plugin->{remote}, $plugin->{local} );
    } else {
        _get_web( $plugin->{remote}, $plugin->{local} );
    }
}

