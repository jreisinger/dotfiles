#!/usr/bin/env perl
use strict;
use warnings;
use File::Path qw(make_path remove_tree);
use IPC::Cmd qw(can_run);
use Getopt::Long;
use Pod::Usage;

#################
# Configuration #
#################

# Command line options
my $help = 0;
my $man  = 0;
GetOptions(
    "help|h|?" => \$help,
    man        => \$man,
) or pod2usage(2);

# Help
pod2usage(1) if $help;
pod2usage( -exitval => 0, -verbose => 2, -noperldoc => 1 ) if $man;

#############
# Functions #
#############

sub _get_web {
    my ( $remote, $local ) = @_;

    # If curl is not there use wget
    print "Donwloading $remote to '$local'\n";
    if ( can_run('wget') ) {
        system "wget -q -O $local $remote" and die;
    } else {
        system "curl -so $local $remote" and die;
    }
}

sub _get_git {
    my ( $remote_repo, $local_repo ) = @_;

    if ( -d $local_repo ) {
        chdir $local_repo and print "Changed to '$local_repo'\n";
        system "git pull";
    } else {
        system "git clone $remote_repo $local_repo";
    }
}

########
# Main #
########

# Create necessary directories
my $autoload_dir = "$ENV{HOME}/.vim/autoload";
make_path $autoload_dir unless -e $autoload_dir;
my $bundle_dir = "$ENV{HOME}/.vim/bundle";
make_path $bundle_dir unless -d $bundle_dir;

my $plugins = [
    {
        name => 'pathogen.vim',
        remote =>
'https://raw.github.com/tpope/vim-pathogen/master/autoload/pathogen.vim',
        local => "$autoload_dir/pathogen.vim",
    },
    {
        name   => 'vim nerd tree',
        remote => 'https://github.com/scrooloose/nerdtree.git',
        local  => "$bundle_dir/nerdtree",
    },
    {
        name   => 'vim nerd tree tabs',
        remote => 'https://github.com/jistr/vim-nerdtree-tabs.git',
        local  => "$bundle_dir/vim-nerdtree-tabs",
    },
    {
        name   => 'vim-markdown',
        remote => 'https://github.com/plasticboy/vim-markdown.git',
        local  => "$bundle_dir/vim-markdown",
    },
    {
        name   => 'BufExplorer',
        remote => 'https://github.com/jlanzarotta/bufexplorer.git',
        local  => "$bundle_dir/bufexplorer",
    },
];

for my $plugin (@$plugins) {
    print "\n--> Install $plugin->{name}\n";
    if ( $plugin->{remote} =~ /\.git$/ ) {
        _get_git( $plugin->{remote}, $plugin->{local} );
    } else {
        _get_web( $plugin->{remote}, $plugin->{local} );
    }
}

__END__

=head1 NAME

install_vim_stuff - install useful vim plugins (from github)

=head1 SYNOPSIS

install_vim_stuff [options]

  Options:
    --help      brief help message
    --man       full documentation

=cut
