#!/usr/bin/perl
# Generate ~/.screenrc_servers that will connect you to all the servers in ~/.servers.txt
use strict;
use warnings;
use autodie;
use Getopt::Long;
use Pod::Usage;

#################
# Configuration #
#################

# Command line options
my $help = 0;
my $man  = 0;
GetOptions(
    "help|h|?" => \$help,
    man        => \$man,
) or pod2usage(2);

# Help
pod2usage(1) if $help;
pod2usage( -exitval => 0, -verbose => 2, -noperldoc => 1 ) if $man;

my $servers          = "$ENV{HOME}/.servers.txt";
my $screenrc_servers = "$ENV{HOME}/.screenrc_servers";

########
# Main #
########

# Add general stuff
open my $screenrc_servers_fh, ">", $screenrc_servers;
print $screenrc_servers_fh q{# define a bigger scrollback, default is 100 lines
defscrollback 15000

# turn sending of screen messages to hardstatus off
hardstatus off

startup_message off

# ------------------------------------------------------------------------------
# STARTUP SCREENS
# ------------------------------------------------------------------------------
#         Screen_name   Screen #        Startup command
};
close $screenrc_servers_fh;

# Add list of servers to connect
if ( -f $servers ) {
    open my $servers_fh,          "<",  $servers;
    open my $screenrc_servers_fh, ">>", $screenrc_servers;
    my $screen = 0;
    while (<$servers_fh>) {
        chomp;
        print $screenrc_servers_fh
          "screen -t $_     $screen               ssh root\@$_\n";
        $screen++;
    }
}

print
  "$screenrc_servers generated. To use it run:\n> screen -S servers -c $screenrc_servers\n";

__END__

=head1 NAME

gen-servers-screenrc - generate ~/.screenrc_servers that will connect you to
all the servers listed in ~/.servers.txt

=head1 SYNOPSIS

gen-servers-screenrc [options]

  Options:
    --help      brief help message
    --man       full documentation

=cut
