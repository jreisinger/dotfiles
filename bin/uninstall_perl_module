#!/usr/bin/perl
use 5.14.2;
use ExtUtils::Installed;
use ExtUtils::Packlist;
use Getopt::Long;
use Pod::Usage;

#################
# Configuration #
#################

# Command line options
my $help = 0;
my $man  = 0;
GetOptions(
    "help|h|?" => \$help,
    man        => \$man,
) or pod2usage(2);

# Help
pod2usage(1) if $help;
pod2usage( -exitval => 0, -verbose => 2, -noperldoc => 1 ) if $man;

########
# Main #
########

# Exit unless a module name was passed
die(
    "Error: no Module::Name passed as an argument. E.G.\n\t perl $0 Module::Name\n"
) unless $#ARGV == 0;

my $module = shift @ARGV;

my $installed_modules = ExtUtils::Installed->new;

# iterate through and try to delete every file associated with the module
foreach my $file ( $installed_modules->files($module) ) {
    print "removing $file\n";
    unlink $file or warn "could not remove $file: $!\n";
}

# delete the module packfile
my $packfile = $installed_modules->packlist($module)->packlist_file;
print "removing $packfile\n";
unlink $packfile or warn "could not remove $packfile: $!\n";

# delete the module directories if they are empty
foreach my $dir ( sort( $installed_modules->directory_tree($module) ) ) {
    print("removing $dir\n");
    rmdir $dir or warn "could not remove $dir: $!\n";
}

__END__

=head1 NAME

uninstall_perl_module - uninstall given Perl module

=head1 SYNOPSIS

uninstall_perl_module [options] Perl::Module

  Options:
    --help      brief help message
    --man       full documentation

=head1 DESCRIPTION

Taken from PerlTricks.com

=cut
